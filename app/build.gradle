apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def secretsProperties = new Properties()
ext {
    file = new File(rootDir, "secrets.properties")
    if (file.exists()) {
        secretsProperties.load(new FileInputStream(file))
    } else {
        secretsProperties.put("partnerToken", "1234567890")
    }
}
androidExtensions {
    experimental = true
}

ext.versionMajor = 2
ext.versionMinor = 3
ext.versionPatch = 2

android {
    compileSdkVersion 30
    useLibrary 'org.apache.http.legacy'
    buildToolsVersion '30.0.3'
    defaultConfig {
        applicationId "com.bumbumapps.radiofm"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 4
        versionName "3.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [toothpick_registry_package_name: 'com.bumbumapps.radiofm',
                             "room.schemaLocation"          :
                                     "$projectDir/schemas".toString()]
            }
        }
        buildConfigField("String", "PARTNER_TOKEN", "\"${secretsProperties['partnerToken']}\"")
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    configurations.all {
        resolutionStrategy.force 'com.android.support:support-v4:24.0.0'
    }
    kapt { generateStubs = true }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            multiDexEnabled true
        }
    }
}

private Integer generateVersionCode() {
    return ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    return "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
}

ext {
    ROOM_VERSION = "2.1.0"
    EXOPLAYER_VERSION = "2.9.6"
    OKHHP_VERSION = "3.12.1"
}

dependencies {
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    //Support
    implementation "androidx.appcompat:appcompat:1.1.0-rc01"
    implementation "com.google.android.material:material:1.1.0-alpha07"
    implementation "androidx.media:media:1.1.0-rc01"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.dynamicanimation:dynamicanimation:1.0.0"
    implementation "androidx.preference:preference:1.1.0-rc01"
    implementation "androidx.legacy:legacy-preference-v14:1.0.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta4'
    //DB
    implementation "androidx.room:room-runtime:$ROOM_VERSION"
    kapt "androidx.room:room-compiler:$ROOM_VERSION"
    implementation "androidx.room:room-rxjava2:$ROOM_VERSION"
    //Net
    implementation "com.squareup.retrofit2:retrofit:2.6.0"
    implementation "com.squareup.retrofit2:converter-gson:2.6.0"
    implementation "com.squareup.okhttp3:okhttp:$OKHHP_VERSION"
    implementation "com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0"
    //Rx
    implementation "io.reactivex.rxjava2:rxjava:2.2.10"
    implementation "io.reactivex.rxjava2:rxkotlin:2.4.0-beta.1"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation "com.jakewharton.rxrelay2:rxrelay:2.1.0"
    //DI
    implementation "com.github.stephanenicolas.toothpick:toothpick-runtime:1.1.3"
    kapt "com.github.stephanenicolas.toothpick:toothpick-compiler:1.1.3"
    //Navigation
    implementation "ru.terrakok.cicerone:cicerone:3.0.0"
    //Image loading
    implementation "com.github.bumptech.glide:glide:4.9.0"
    implementation "com.github.bumptech.glide:annotations:4.9.0"
    kapt "com.github.bumptech.glide:compiler:4.9.0"
    implementation "com.github.bumptech.glide:okhttp3-integration:4.9.0"
    implementation "com.github.mkaflowski:Media-Style-Palette:1.3"
    //Logging
    implementation "com.jakewharton.timber:timber:4.7.1"
    implementation "com.facebook.stetho:stetho:1.5.1"
    implementation "com.squareup.okhttp3:logging-interceptor:$OKHHP_VERSION"
    //ExoPlayer
    implementation "com.google.android.exoplayer:exoplayer-core:$EXOPLAYER_VERSION"
    implementation "com.google.android.exoplayer:extension-okhttp:$EXOPLAYER_VERSION"

    //ads
    implementation 'com.google.android.gms:play-services-ads:8.4.0'

    implementation project(':playerbutton')
}
